<!DOCTYPE HTML>
<html>

<head>
  <title>eNGE</title>
  <link rel="stylesheet" type="text/css" href="index-webgl2.css">

  <script type="text/javascript" src="base64.js"></script>
  <script type="text/javascript" src="trace.js"></script>
  <script type="text/javascript" src="utils.js"></script>

  <script type="text/javascript" src="psx-core/core.js"></script>
  <script type="text/javascript" src="psx-core/config.js"></script>

  <script type="text/javascript" src="psx-renderers/webgl2-utils.js"></script>
  <script type="text/javascript" src="psx-renderers/webGL2HQ-buffers.js"></script>
  <script type="text/javascript" src="psx-renderers/webGl2HQ.js"></script>

  <script type="text/javascript" src="psx-core/memory.js"></script>
  <script type="text/javascript" src="psx-core/enge.js"></script>
  <script type="text/javascript" src="psx-core/cdrom.js"></script>
  <script type="text/javascript" src="psx-core/recompiler.js"></script>
  <script type="text/javascript" src="psx-core/directMemoryAccess.js"></script>
  <script type="text/javascript" src="psx-core/geometryTransformEngine.js"></script>
  <script type="text/javascript" src="psx-core/graphicsProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/macroBlockDecoder.js"></script>
  <script type="text/javascript" src="psx-core/serialPorts.js"></script>
  <script type="text/javascript" src="psx-core/soundProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/rootCounters.js"></script>
  <script type="text/javascript" src="psx-core/gamepad.js"></script>

  <script type="text/javascript" src="index.js"></script>

  <script id="vram-direct" type="x-shader/x-vertex">
    #version 300 es

    precision highp float;
    precision highp int;

    out vec3 v_position;
    out vec4 v_color;

    in vec3 a_position;
    in vec4 a_color;

    void main() {
      gl_Position = vec4(a_position, 1.0);
      gl_Position.x = (gl_Position.x - 512.0) / +512.0;
      gl_Position.y = (gl_Position.y - 256.0) / +256.0;
      gl_Position.z = a_position.z / 65536.0;

      v_position = a_position;
      v_color = a_color;
    }
  </script>

  <script id="vram-direct" type="x-shader/x-fragment">
    #version 300 es

    precision highp float;
    precision highp int;

    uniform sampler2D u_texture;

    layout(location = 0) out vec4 outMain;
    layout(location = 1) out vec4 outShadow;

    in vec3 v_position;
    in vec4 v_color;

    void main() {
      vec4 tex = texelFetch(u_texture, ivec2(int(v_position.x) % 4096, int(v_position.y) % 2048), 0);
      outMain = outShadow = tex;
    }
  </script>

  <script id="vram-render" type="x-shader/x-vertex">
    #version 300 es

    precision highp float;

    in vec3 a_position;
    in vec2 a_texcoord;
    in vec4 a_color;

    out vec3 v_drawPos;
    out vec4 v_color;

    void main() {
      gl_Position = vec4(a_position, 1.0);
      gl_Position.x = (gl_Position.x - 512.0) / +512.0;
      gl_Position.y = (gl_Position.y - 256.0) / +256.0;
      gl_Position.z = a_position.z / 65536.0;

      v_drawPos = a_position.xyz;
      v_color = vec4(a_color.rgb, 1.0);
    }
  </script>

  <script id="vram-render" type="x-shader/x-fragment">
    #version 300 es

    precision highp float;
    precision highp int;

    uniform sampler2D u_vram;
    uniform ivec4 u_draw;
    uniform float u_alpha;

    in vec3 v_drawPos;
    in vec4 v_color;

    out vec4 outColor;

    void main() {
      ivec3 texel = ivec3(v_drawPos);

      if (v_color == vec4(0.0, 0.0, 0.0, 0.0)) discard;

      if (texel.x >= u_draw.x && texel.y >= u_draw.y && texel.x <= u_draw.z && texel.y <= u_draw.w) {
        outColor = vec4(v_color.rgb, u_alpha);
        //outColor = vec4(gl_FragCoord.zzz, u_alpha);
      }
      else discard;
    }
  </script>

  <script id="vram-display" type="x-shader/x-vertex">
    #version 300 es

    precision highp float;

    in vec2 a_position;
    in vec2 a_texcoord;
    in vec4 a_color;

    out vec2 v_texcoord;
    out vec2 v_vramCoord;
    out vec4 v_color;
    out vec2 v_pixelCoord;

    void main() {
      gl_Position = vec4(a_position, 0.0, 1.0);
      gl_Position.x = (gl_Position.x - 2048.0) / +2048.0;
      gl_Position.y = (gl_Position.y - 1024.0) / -1024.0;
      v_vramCoord = a_texcoord;
      v_pixelCoord = a_position;
      v_texcoord = a_texcoord * vec2(4.0, 4.0); // texture is 4096x2048 iso 1024x512
      v_color = a_color;
    }
  </script>

  <script id="vram-display" type="x-shader/x-fragment">
    #version 300 es

    precision highp float;
    precision highp int;

    uniform sampler2D u_texture;
    uniform ivec4     u_disp;
    uniform int       u_mode;

    in vec2 v_texcoord;
    in vec2 v_vramCoord;
    in vec4 v_color;

    out vec4 outColor;

    int getSBGRfromRGBA(vec4 c) {
      int sbgr = 0;
      sbgr += int(c.r * 32.0);
      sbgr += int(c.g * 32.0) * 32;
      sbgr += int(c.b * 32.0) * 32 * 32;
      if (c.a >= 0.5) sbgr += 32768;
      return sbgr;
    }

    vec4 InterleavedGradientNoise() {
      vec2 uv = floor(v_vramCoord - vec2(u_disp.xy));

      const vec3 magic = vec3( 0.06711056, 0.00583715, 52.9829189 );
      float rnd = fract(magic.z * fract( dot( uv, magic.xy ) ) ) * (1.0 / 48.0);
      return vec4(rnd, rnd, rnd, 1.0);
    }

    vec4 tv_effect(vec3 col) {
      vec2 resolution = vec2(u_disp.zw - u_disp.xy + ivec2(1,1));
      vec2 uv = (v_vramCoord - vec2(u_disp.xy)) / resolution;

      col *= step(0.0, uv.x) * step(0.0, uv.y);
      col *= 1.0 - step(1.0, uv.x) * 1.0 - step(1.0, uv.y);

      col *= 0.5 + 0.5 * 16.0 * uv.x * uv.y * (1.0 - uv.x) * (1.0 - uv.y);
      col *= vec3(0.98, 1.02, 0.98);

      if (resolution.y <= 288.0) {
        col -= ((int(uv.y * resolution.y * 2.0) & 1) == 0) ? 0.0 : 0.10; //scanline
        col -= ((int(uv.x * resolution.x * 2.0) & 1) == 0) ? 0.0 : 0.05; //pixel
      }
      else {
        col -= ((int(uv.y * resolution.y) & 1) == 0) ? 0.0 : 0.10; //scanline
        col -= ((int(uv.x * resolution.x * 2.0) & 1) == 0) ? 0.0 : 0.05; //pixel
      }

      return vec4(col, 1.0);
    }

    vec4 vram24Bit() {
      ivec2 ds = ivec2(v_vramCoord) - u_disp.xy;

      float tx = float(u_disp.x) + floor(float(ds.x) * 1.5);
      float ty = floor(v_texcoord.y);

      vec4 c1 = texelFetch(u_texture, ivec2(4.0 * tx + 0.0, ty), 0);
      int  sbgr1 = getSBGRfromRGBA(c1);
      vec4 c2 = texelFetch(u_texture, ivec2(4.0 * tx + 4.0, ty), 0);
      int  sbgr2 = getSBGRfromRGBA(c2);

      if ((ds.x & 1) == 1) {
        float r = float((sbgr1 >> 8) & 255) / 256.0;
        float g = float((sbgr2 >> 0) & 255) / 256.0;
        float b = float((sbgr2 >> 8) & 255) / 256.0;

        return vec4(r, g, b, 1.0);
      }
      else {
        float r = float((sbgr1 >> 0) & 255) / 256.0;
        float g = float((sbgr1 >> 8) & 255) / 256.0;
        float b = float((sbgr2 >> 0) & 255) / 256.0;

        return vec4(r, g, b, 1.0);
      }
    }

    vec4 vram16Bit() {
      return texelFetch(u_texture, ivec2(v_texcoord), 0);
    }

    void main() {
      vec4 color = vec4(0.0, 0.0, 0.0, 0.0);

      switch (u_mode & 7) {
        case 1: color = vram24Bit(); break;
        case 0: color = vram16Bit(); break;
      }

      outColor = tv_effect(color.rgb + InterleavedGradientNoise().rgb);
    }
  </script>
</head>

<body onload="init();">
  <canvas id="ambilight" width="4096" height="2048"></canvas>
  <div id="background"></div>
  <canvas id="display" width="4096" height="2048"></canvas>
</body>

</html>