<!DOCTYPE HTML>
<html>

<head>
  <title>eNGE</title>
  <link rel="stylesheet" type="text/css" href="index-webgl2.css">

  <script type="text/javascript" src="base64.js"></script>
  <script type="text/javascript" src="trace.js"></script>
  <script type="text/javascript" src="utils.js"></script>

  <script type="text/javascript" src="psx-core/core.js"></script>
  <script type="text/javascript" src="psx-core/config.js"></script>

  <script type="text/javascript" src="psx-renderers/webgl2-utils.js"></script>
  <script type="text/javascript" src="psx-renderers/webGl2HQ.js"></script>

  <script type="text/javascript" src="psx-core/memory.js"></script>
  <script type="text/javascript" src="psx-core/enge.js"></script>
  <script type="text/javascript" src="psx-core/cdrom.js"></script>
  <script type="text/javascript" src="psx-core/recompiler.js"></script>
  <script type="text/javascript" src="psx-core/directMemoryAccess.js"></script>
  <script type="text/javascript" src="psx-core/geometryTransformEngine.js"></script>
  <script type="text/javascript" src="psx-core/graphicsProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/macroBlockDecoder.js"></script>
  <script type="text/javascript" src="psx-core/serialPorts.js"></script>
  <script type="text/javascript" src="psx-core/soundProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/rootCounters.js"></script>
  <script type="text/javascript" src="psx-core/gamepad.js"></script>

  <script type="text/javascript" src="index.js"></script>

  <script id="pixel" type="x-shader/x-vertex">#version 300 es
    precision highp float;
    precision highp int;

    in vec2 a_position;
    in vec2 a_texcoord;
    in vec4 a_color;
    in vec4 a_twin;
    in float a_tmode;
    in float a_clut;

    out vec2 v_vramCoord;
    out vec4 v_color;
    out vec2 v_pixelCoord;
    flat out float v_textureMode;
    flat out ivec2 v_texturePage;
    flat out uint packetId;
    flat out ivec2 clut;
    flat out ivec4 twin;

    void main() {
      gl_Position = vec4(a_position, 0.0, 1.0);
      gl_Position.x = (gl_Position.x - 512.0) / +512.0;
      gl_Position.y = (gl_Position.y - 256.0) / +256.0;
      v_vramCoord = a_texcoord; // texture is 4096x2048
      v_pixelCoord = a_position;
      v_color = vec4(a_color.rgb, 1.0);
      v_textureMode = mod(a_tmode, 4.0);
      float tpx = mod(floor(a_tmode / 4.0), 16.0);
      float tpy = mod(floor(a_tmode / 64.0), 2.0);
      v_texturePage = ivec2(int(tpx * 256.0), tpy * 1024.0);
      packetId = uint(a_color.a * 255.0);
      float cx = mod(floor(a_clut / 1.0), 64.0) * 16.0;
      float cy = mod(floor(a_clut / 64.0), 512.0);
      clut = ivec2(int(cx), int(cy));
      twin = ivec4(a_twin);
    }
  </script>

  <script id="videoram" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision highp int;

    uniform sampler2D u_texture;
    uniform float u_alpha;
    uniform ivec4 u_draw;

    in vec2 v_vramCoord;
    in vec4 v_color;
    in vec2 v_pixelCoord;
    flat in float v_textureMode;
    flat in ivec2 v_texturePage;
    flat in uint packetId;
    flat in ivec2 clut;
    flat in ivec4 twin;

    out vec4 outColor;

    int getSBGRfromRGBA(vec4 c) {
      int sbgr = 0;
      sbgr += int(c.r * 32.0);
      sbgr += int(c.g * 32.0) * 32;
      sbgr += int(c.b * 32.0) * 32 * 32;
      if (c.a >= 0.5) sbgr += 32768;
      return sbgr;
    }

    vec4 vram16Bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(4,4) * ivec2(tx, ty);
      return texelFetch(u_texture, ivec2(coord.x % 4096, coord.y % 2048), 0);
    }

    vec4 vram8bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(2,4) * ivec2(tx, ty);
      vec4 color = texelFetch(u_texture, ivec2(coord.x % 4096, coord.y % 2048), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(tx) & 1;
      int clutIndex = (sbgr >> (bit * 8)) & 0xff;

      coord = ivec2(4,4) * (clut + ivec2(clutIndex, 0));
      return texelFetch(u_texture, ivec2(coord.x % 4096, coord.y % 2048), 0);
    }

    vec4 vram4bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(1,4) * ivec2(tx, ty);
      vec4 color = texelFetch(u_texture, ivec2(coord.x % 4096, coord.y % 2048), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(tx) & 3;
      int clutIndex = (sbgr >> (bit * 4)) & 0xf;

      coord = ivec2(4,4) * (clut + ivec2(clutIndex, 0));
      return texelFetch(u_texture, ivec2(coord.x % 4096, coord.y % 2048), 0);
    }

    void main() {
      ivec2 texel = ivec2(v_pixelCoord).xy;

      if (texel.x >= u_draw.x && texel.y >= u_draw.y && texel.x <= u_draw.z && texel.y <= u_draw.w) {
        if (0x00u == (packetId & 0x04u)) {
          outColor = vec4(v_color.rgb, u_alpha);
        }
        else {
          vec4 tc;

          int tx = ((int(v_vramCoord.x) & 255) & ~twin.x) | (twin.z & twin.x);
          int ty = ((int(v_vramCoord.y) & 255) & ~twin.y) | (twin.a & twin.y);

          if (v_textureMode == 0.0) tc = vram4bit(tx, ty);
          if (v_textureMode == 1.0) tc = vram8bit(tx, ty);
          if (v_textureMode == 2.0) tc = vram16Bit(tx, ty);
          if (v_textureMode == 3.0) tc = vram16Bit(tx, ty);

          if (tc.rgba == vec4(0.0, 0.0, 0.0, 0.0)) discard;
          
          if (0u == (packetId & 1u)) {
            // shaded vertex, opaque, texture-blending
            outColor = vec4(tc.rgb * (v_color.rgb / vec3(0.5, 0.5, 0.5)), u_alpha);
          }
          else {
            // vertex, opaque, texture-raw
            outColor = vec4(tc.rgb, u_alpha);
          }
        }
      }
      else discard;
    }
  </script>

  <script id="vertex" type="x-shader/x-vertex">#version 300 es
    precision highp float;
  
    in vec2 a_position;
    in vec2 a_texcoord;
    in vec4 a_color;
   
    out vec2 v_texcoord;
    out vec2 v_vramCoord;
    out vec4 v_color;
    out vec2 v_pixelCoord;
  
    void main() {
      gl_Position = vec4(a_position, 0.0, 1.0);
      gl_Position.x = (gl_Position.x - 512.0) / +512.0;
      gl_Position.y = (gl_Position.y - 256.0) / -256.0;
      v_vramCoord = a_texcoord; 
      v_pixelCoord = a_position;
      v_texcoord = a_texcoord * vec2(4.0, 4.0); // texture is 4096x2048 iso 1024x512
      v_color = a_color;
    }
  </script>

  <script id="displayScreen" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision highp int;
     
    uniform sampler2D u_texture;
    uniform ivec4     u_disp;
    uniform ivec4     u_draw;
    uniform float     u_time;
    uniform int       u_mode;
  
    in vec2 v_texcoord;
    in vec2 v_vramCoord;
    in vec4 v_color;

    out vec4 outColor;
   
    int getSBGRfromRGBA(vec4 c) {
      int sbgr = 0;
      sbgr += int(c.r * 32.0);
      sbgr += int(c.g * 32.0) * 32;
      sbgr += int(c.b * 32.0) * 32 * 32;
      if (c.a >= 0.5) sbgr += 32768;
      return sbgr;
    }
  
    vec4 InterleavedGradientNoise() {
      vec2 uv = floor(v_vramCoord - vec2(u_disp.xy));

      const vec3 magic = vec3( 0.06711056, 0.00583715, 52.9829189 );
      float rnd = fract(magic.z * fract( dot( uv, magic.xy ) ) ) * (1.0 / 48.0);
      return vec4(rnd, rnd, rnd, 1.0);
    }
  
    vec4 tv_effect(vec3 col) {
      vec2 resolution = vec2(u_disp.zw - u_disp.xy + ivec2(1,1));
      vec2 uv = (v_vramCoord - vec2(u_disp.xy)) / resolution;

      col *= step(0.0, uv.x) * step(0.0, uv.y);
      col *= 1.0 - step(1.0, uv.x) * 1.0 - step(1.0, uv.y);
    
      col *= 0.5 + 0.5 * 16.0 * uv.x * uv.y * (1.0 - uv.x) * (1.0 - uv.y);
      col *= vec3(0.98, 1.02, 0.98);
    
      if (resolution.y <= 288.0) {
        col -= ((int(uv.y * resolution.y * 2.0) & 1) == 0) ? 0.0 : 0.10; //scanline
        col -= ((int(uv.x * resolution.x * 2.0) & 1) == 0) ? 0.0 : 0.05; //pixel
      }
      else {
        col -= ((int(uv.y * resolution.y) & 1) == 0) ? 0.0 : 0.10; //scanline
        col -= ((int(uv.x * resolution.x * 2.0) & 1) == 0) ? 0.0 : 0.05; //pixel
      }

      return vec4(col, 1.0);
    }
    
    vec4 vram24Bit() {
      ivec2 ds = ivec2(v_vramCoord) - u_disp.xy;
  
      float tx = float(u_disp.x) + floor(float(ds.x) * 1.5);
      float ty = floor(v_texcoord.y);
  
      vec4 c1 = texelFetch(u_texture, ivec2(4.0 * tx + 0.0, ty), 0);
      int  sbgr1 = getSBGRfromRGBA(c1);
      vec4 c2 = texelFetch(u_texture, ivec2(4.0 * tx + 4.0, ty), 0);
      int  sbgr2 = getSBGRfromRGBA(c2);
  
      if ((ds.x & 1) == 1) {
        float r = float((sbgr1 >> 8) & 255) / 256.0;
        float g = float((sbgr2 >> 0) & 255) / 256.0;
        float b = float((sbgr2 >> 8) & 255) / 256.0;
    
        return vec4(r, g, b, 1.0);
      }
      else {
        float r = float((sbgr1 >> 0) & 255) / 256.0;
        float g = float((sbgr1 >> 8) & 255) / 256.0;
        float b = float((sbgr2 >> 0) & 255) / 256.0;
    
        return vec4(r, g, b, 1.0);
      }
    }

    vec4 vram16Bit() {
      return texelFetch(u_texture, ivec2(v_texcoord), 0);
    }

    vec4 vram8bit() {
      vec4 color = texelFetch(u_texture, ivec2(v_texcoord), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(v_texcoord.x) & 1;
      int clutIndex = (sbgr >> (bit * 8)) & 0xff;

      float v = float(clutIndex) / 255.0;
      return vec4(v, v, v, 1.0); 
    }

    vec4 vram4bit() {
      vec4 color = texelFetch(u_texture, ivec2(v_texcoord), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(v_texcoord.x) & 3;
      int clutIndex = (sbgr >> (bit * 4)) & 0xf;

      float v = float(clutIndex) / 15.0;
      return vec4(v, v, v, 1.0); 
    }

    vec4 gpuState() {
      ivec2 texel = ivec2(v_vramCoord).xy;

      vec4 color = texelFetch(u_texture, ivec2(v_texcoord), 0);
      float r = 0.0, g = 0.0, b = 0.0;
      if (texel.x >= u_draw.x && texel.y >= u_draw.y && texel.x <= u_draw.z && texel.y <= u_draw.w) {
        if (texel.x == u_draw.x || texel.x == u_draw.z) color.rgb = vec3(1.0, 0.0, 0.0);
        if (texel.y == u_draw.y || texel.y == u_draw.w) color.rgb = vec3(1.0, 0.0, 0.0);
      }
      if (texel.x >= u_disp.x && texel.y >= u_disp.y && texel.x <= u_disp.z && texel.y <= u_disp.w) {
        if (texel.x == u_disp.x || texel.x == u_disp.z) color.rgb = vec3(0.0, 0.0, 1.0);
        if (texel.y == u_disp.y || texel.y == u_disp.w) color.rgb = vec3(0.0, 0.0, 1.0);
      }
      return color;
    }

    void main() {
      vec4 color = vec4(0.0, 0.0, 0.0, 0.0);

      switch (u_mode) {
        case 7: outColor = gpuState(); return;

        case 1: color = vram24Bit(); break;
        case 0: color = vram16Bit(); break;
        case 6: color = vram16Bit(); break;
        case 2: color = vram8bit(); break;
        case 3: color = vram4bit(); break;
      }

      color = color * v_color;

      if (u_mode <= 1) {
        outColor = tv_effect(color.rgb + InterleavedGradientNoise().rgb);
      }
      else {
        outColor = color;
      }
    }
    </script>
</head>

<body onload="init();">
  <canvas id="ambilight"></canvas>
  <div id="background"></div>
  <canvas id="display"></canvas>
  <!-- <div id="menu"></div> -->
</body>

</html>