<!DOCTYPE HTML>
<html>

<head>
  <title>eNGE</title>
  <link rel="stylesheet" type="text/css" href="index-webgl2.css">

  <script type="text/javascript" src="base64.js"></script>
  <script type="text/javascript" src="trace.js"></script>
  <script type="text/javascript" src="utils.js"></script>

  <script type="text/javascript" src="psx-core/core.js"></script>
  <script type="text/javascript" src="psx-core/config.js"></script>

  <script type="text/javascript" src="psx-renderers/webgl2-utils.js"></script>
  <script type="text/javascript" src="psx-renderers/webGl2HQ.js"></script>

  <script type="text/javascript" src="psx-core/memory.js"></script>
  <script type="text/javascript" src="psx-core/enge.js"></script>
  <script type="text/javascript" src="psx-core/cdrom.js"></script>
  <script type="text/javascript" src="psx-core/recompiler.js"></script>
  <script type="text/javascript" src="psx-core/directMemoryAccess.js"></script>
  <script type="text/javascript" src="psx-core/geometryTransformEngine.js"></script>
  <script type="text/javascript" src="psx-core/graphicsProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/macroBlockDecoder.js"></script>
  <script type="text/javascript" src="psx-core/serialPorts.js"></script>
  <script type="text/javascript" src="psx-core/soundProcessingUnit.js"></script>
  <script type="text/javascript" src="psx-core/rootCounters.js"></script>
  <script type="text/javascript" src="psx-core/gamepad.js"></script>

  <script type="text/javascript" src="index.js"></script>

  <script id="pixel" type="x-shader/x-vertex">#version 300 es
    precision highp float;
    precision highp int;

    in vec2 a_position;
    in vec2 a_texcoord;
    in vec4 a_color;
    in vec4 a_twin;
    in float a_tmode;
    in float a_clut;

    out vec2 v_vramCoord;
    out vec4 v_color;
    out vec2 v_pixelCoord;
    flat out uint v_textureMode;
    flat out ivec2 v_texturePage;
    flat out uint packetId;
    flat out ivec2 clut;
    flat out ivec4 twin;

    void main() {
      gl_Position = vec4(a_position, 0.0, 1.0);
      gl_Position.x = (gl_Position.x - 512.0) / +512.0;
      gl_Position.y = (gl_Position.y - 256.0) / +256.0;
      v_vramCoord = a_texcoord; // texture is 1024x512
      v_pixelCoord = a_position;
      v_color = vec4(a_color.rgb, 1.0);
      v_textureMode = uint(mod(a_tmode, 4.0));
      float tpx = mod(floor(a_tmode / 4.0), 16.0);
      float tpy = mod(floor(a_tmode / 64.0), 2.0);
      v_texturePage = ivec2(int(tpx * 64.0), tpy * 256.0);
      packetId = uint(a_color.a * 255.0);
      float cx = mod(floor(a_clut / 1.0), 64.0) * 16.0;
      float cy = mod(floor(a_clut / 64.0), 512.0);
      clut = ivec2(int(cx), int(cy));
      twin = ivec4(a_twin);
    }
  </script>

  <script id="videoram" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision highp int;

    uniform sampler2D u_texture;
    uniform float u_alpha;
    uniform ivec4 u_draw;

    in vec2 v_vramCoord;
    in vec4 v_color;
    in vec2 v_pixelCoord;
    flat in uint v_textureMode;
    flat in ivec2 v_texturePage;
    flat in uint packetId;
    flat in ivec2 clut;
    flat in ivec4 twin;

    out vec4 outColor;

    int getSBGRfromRGBA(vec4 c) {
      int sbgr = 0;
      sbgr += int(c.r * 32.0);
      sbgr += int(c.g * 32.0) * 32;
      sbgr += int(c.b * 32.0) * 32 * 32;
      if (c.a >= 0.5) sbgr += 32768;
      return sbgr;
    }

    vec4 vram16Bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(tx, ty);
      return texelFetch(u_texture, ivec2(coord.x % 1024, coord.y % 512), 0);
    }

    vec4 vram8bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(tx >> 1, ty);
      vec4 color = texelFetch(u_texture, ivec2(coord.x % 1024, coord.y % 512), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(tx) & 1;
      int clutIndex = (sbgr >> (bit * 8)) & 0xff;

      coord = (clut + ivec2(clutIndex, 0));
      return texelFetch(u_texture, ivec2(coord.x % 1024, coord.y % 512), 0);
    }

    vec4 vram4bit(int tx, int ty) {
      ivec2 coord = ivec2(v_texturePage) + ivec2(tx >> 2, ty);
      vec4 color = texelFetch(u_texture, ivec2(coord.x % 1024, coord.y % 512), 0);
      int sbgr = getSBGRfromRGBA(color);

      int bit = int(tx) & 3;
      int clutIndex = (sbgr >> (bit * 4)) & 0xf;

      coord = (clut + ivec2(clutIndex, 0));
      return texelFetch(u_texture, ivec2(coord.x % 1024, coord.y % 512), 0);
    }

    void main() { 
      ivec2 texel = ivec2(v_pixelCoord).xy;

      if (texel.x >= u_draw.x && texel.y >= u_draw.y && texel.x <= u_draw.z && texel.y <= u_draw.w) {
        bool texturedPrimitive = 0x04u == (packetId & 0x04u);
        bool transparentPrimitive = 0x02u == (packetId & 0x02u);

        if (!texturedPrimitive) {
          outColor = vec4(v_color.rgb, u_alpha);
        }
        else {
          vec4 tc;

          int tx = ((int(v_vramCoord.x) & 255) & ~twin.x) | (twin.z & twin.x);
          int ty = ((int(v_vramCoord.y) & 255) & ~twin.y) | (twin.a & twin.y);

          if (v_textureMode == 0u) tc = vram4bit(tx, ty);
          if (v_textureMode == 1u) tc = vram8bit(tx, ty);
          if (v_textureMode == 2u) tc = vram16Bit(tx, ty);
          if (v_textureMode == 3u) tc = vram16Bit(tx, ty);

          if (tc.rgba == vec4(0.0, 0.0, 0.0, 0.0)) discard;

          vec3 blendingColor = 0u == (packetId & 1u) ? v_color.rgb / vec3(0.5, 0.5, 0.5) : vec3(1.0,1.0,1.0);
          bool transparentPixel = tc.a > 0.5;
          bool special = 0x80u == (packetId & 0x80u); // only for semi-transparent textured polygon/rectangle 

          if (special) {
            if (transparentPrimitive && transparentPixel) {
              outColor = vec4(tc.rgb * blendingColor, u_alpha);
            }
            else if (!transparentPrimitive && !transparentPixel) {
              outColor = vec4(tc.rgb * blendingColor, 0.0);
            }
            else {
              discard;
            }
          }
          else {
            outColor = vec4(tc.rgb * blendingColor, u_alpha);
          }
        }
      }
      else discard;
    }
  </script>

</head>

<body onload="init();">
  <canvas id="ambilight"></canvas>
  <div id="background"></div>
  <canvas id="display"></canvas>
  <!-- <div id="menu"></div> -->
</body>

</html>